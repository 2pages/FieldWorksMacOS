<?xml version="1.0" encoding="UTF-8"?>
<!-- This xslt generates the inventory of default parts, one for each property in the model -->

<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
			xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="msxsl">
	<xsl:output method="xml" version="1.0" encoding="UTF-8" indent="yes"/>

	<xsl:variable name="FieldsToShowInAnalysisWritingSystem">Name Description Abbreviation Comment</xsl:variable>

	<!-- The top level is a LayoutInventory node -->
	<xsl:template match="/">
		<xsl:comment>********* DO NOT MODIFY! This file is generated by PartGenerate.xslt *********</xsl:comment>
		<LayoutInventory>
			<xsl:apply-templates/>
		</LayoutInventory>
	</xsl:template>

	<!-- the top-level input node is a CellarModule containing class elements -->
	<xsl:template match="CellarModule">
		<xsl:apply-templates/>
	</xsl:template>

	<!-- Generate a layout for every class  -->
	<xsl:template match="class">
		<xsl:element name="layout">
			<xsl:attribute name="class">
				<xsl:value-of select="@id"/>
			</xsl:attribute>
			<xsl:attribute name="type">detail</xsl:attribute>
			<xsl:attribute name="name">default</xsl:attribute>
			<xsl:apply-templates mode="classProperties" select="."/>
		</xsl:element>
		<xsl:element name="layout">
			<xsl:attribute name="class">
				<xsl:value-of select="@id"/>
			</xsl:attribute>
			<xsl:attribute name="type">jtview</xsl:attribute>
			<xsl:attribute name="name">default</xsl:attribute>
			<xsl:apply-templates mode="classProperties" select="."/>
		</xsl:element>
	</xsl:template>

	<!-- this builds part refs for each property. It inserts parts for superclass properties
	first by a recursive 'call' -->
	<xsl:template match="class" mode="classProperties">
		<xsl:if test="@base != 'CmObject'">
			<xsl:variable name="mySuper" select="@base"/>
			<xsl:apply-templates select="//class[@id = $mySuper]" mode="classProperties"/>
		</xsl:if>
		<xsl:apply-templates />
	</xsl:template>

	<xsl:template match="props/*">
		<xsl:element name="part">
			<xsl:attribute name="ref"><xsl:value-of select="@id"/></xsl:attribute>
		</xsl:element>
	</xsl:template>

</xsl:stylesheet>
